export DOCKER_USERID := `id -u`
export DOCKER_GROUPID := `id -g`

# Load .env files by default
set dotenv-load := true

# enable modern docker build features
export DOCKER_BUILDKIT := "1"
export COMPOSE_DOCKER_CLI_BUILD := "1"

export BIN := "/opt/venv/bin"

build env="dev":
    #!/usr/bin/env bash

    # set build args for prod builds
    export BUILD_DATE=$(date -u +'%y-%m-%dT%H:%M:%SZ')
    export GITREF=$(git rev-parse --short HEAD)

    # build the thing
    docker-compose build --pull {{ env }}

# copy built node assets back to host
copy-assets: build
    #!/usr/bin/env bash
    docker-compose build --pull node-assets
    docker-compose run --rm --no-deps --entrypoint /usr/bin/env node-assets rsync --delete -r /usr/src/app/assets/dist/ /app/assets/dist/


# run tests in docker container
test *args="": copy-assets
    docker-compose up --detach db
    docker-compose run --rm test bash -c "python -m coverage run --module pytest {{ args }} && python -m coverage report || python -m coverage html"


# run dev server in docker container
serve env="dev" *options="": copy-assets
    docker-compose up --detach db
    docker-compose up {{ options }} {{ env }}


# run command in container
run env="dev" *args="bash": copy-assets
    docker-compose up --detach db
    docker-compose run {{ env }} {{ args }}


# exec command in existing container
exec env="dev" *args="bash": copy-assets
    docker-compose exec {{ env }} {{ args }}


# run a basic functional smoke test against a running container
smoke-test host="http://localhost:8000":
    #!/bin/bash
    set -eu
    curl -I {{ host }} -s --compressed --fail --retry 20 --retry-delay 1 --retry-all-errors
